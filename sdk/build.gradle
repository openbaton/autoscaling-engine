/*
 * Copyright (c) 2015 Fraunhofer FOKUS
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}
plugins {
    id 'net.researchgate.release' version '2.1.2'
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = ''
    preTagCommitMessage = 'Pre-tag commit: '
    tagCommitMessage = 'Creating tag: '
    newVersionCommitMessage = 'New version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    versionProperties = []
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

project.ext {
    //------------------------------------------------//
    /**
     * Spring Related Variables
     */
    springVersion = '4.2.0.RELEASE'
    springBootVersion = '1.2.5.RELEASE'
    //------------------------------------------------//
    //------------------------------------------------//
    /**
     * Application Related Version
     */
    applicationVersion = "0.6-alpha"
    applicationVersionDebian = "1";

    //------------------------------------------------//
}
subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'org.openbaton'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    sourceSets {
        main {
            java      { srcDir 'src/main/java'}
            resources { srcDir 'src/main/resources'}
        }

        test {
            java      { srcDir 'src/test/java'}
            resources { srcDir 'src/test/resources'}
        }

        integration {
            java      { srcDir 'src/test/integration/java' }
            resources { srcDir 'src/test/resources'}
            compileClasspath += sourceSets.test.compileClasspath
            runtimeClasspath += sourceSets.test.runtimeClasspath
            output.classesDir = sourceSets.test.output.classesDir
        }

    }

    dependencies {
        integrationCompile sourceSets.test.output
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://193.175.132.176:8081/nexus/content/groups/public"
    }
}

dependencies {
    compile 'org.slf4j:slf4j-simple:1.7.12'
}

project(':sdk') {
    apply plugin: 'com.bmuschko.nexus'

//    dependencies {
//        compile 'org.slf4j:slf4j-simple:1.7.12'
//    }

    modifyPom {
        project {
            name 'OpenBaton'
            description 'Openbaton libraries for almost everything'
            url 'http://openbaton.github.io'
            inceptionYear '2015'
            scm {
                url 'http://openbaton.github.io'
                connection ''
                developerConnection 'https://github.com/openbaton'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'lto'
                    name 'Lorenzo Tomasini'
                    email 'lorenzo.tomasini@fokus.fraunhofer.de'
                }
                developer {
                    id 'gca'
                    name 'Giuseppe Carella'
                    email 'giuseppe.carella@fokus.fraunhofer.de'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = false
        javadoc = true
    }

    nexus {
        sign = false
        repositoryUrl = 'http://get.openbaton.org:8081/nexus/content/repositories/thirdparty/'
        snapshotRepositoryUrl = 'http://get.openbaton.org:8081/nexus/content/repositories/snapshots/'
    }
}
